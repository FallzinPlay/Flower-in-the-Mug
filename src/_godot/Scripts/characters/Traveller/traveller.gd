class_name Traveller
extends CharacterBody2D

const SPEED = 100

# Estados
enum States {
	IDLE, MOVING, ORDERING, SITTING, DRINKING
}
var current_state: States = States.IDLE
var is_served: bool = false
var is_drink_finished: bool = false
var wants_another_drink: bool = true

# LocalizaÃ§Ã£o
enum Places {
	TAVERN
}
var current_place: Places = Places.TAVERN

# NÃ³s
var drink_ordered: Drink = null
var current_seat: Marker2D = null

# PrÃ© carregados
@onready var path_agent := $Navigator # A*
@onready var timer := $Timer


############ Ciclo Principal ###########
func _process(delta: float) -> void:
	# Se nÃ£o estiver na taverna nÃ£o executa o cÃ³digo
	if current_place != Places.TAVERN:
		return

	# gerenciador de estados
	match current_state:
		# Estado ocioso
		States.IDLE:
			# Se quiser beber mais
			if wants_another_drink:
				# Se nÃ£o tiver servido ou pedido uma bebida, ir atÃ© o bar
				if not is_served and not drink_ordered:
					_move_to_bar()
				# Se estiver servido ir para a mesa
				elif is_served:
					_move_to_seat()
			# Se nÃ£o quiser mais beber
			else:
				# Vai embora
				_empty_seat()
				_move_to_door()
		
		# Estado de movimento
		States.MOVING:
			# Se tiver chegado ao destino parar
			if path_agent.is_navigation_finished():
				velocity = Vector2.ZERO
				_on_arrival()
		
		# Estado de solicitaÃ§Ã£o
		States.ORDERING:
			# Pedir uma bebida
			_request_drink()
		
		# Estado de sentado
		States.SITTING:
			# Pode ter lÃ³gica de esperar X segundos antes de beber
			# ...
			
			var _min_drink_time: float = 10
			var _max_drink_time: float = 30
			# ComeÃ§a a beber
			print("%s estÃ¡ bebendo ðŸº" % name)
			timer.start(randf_range(_min_drink_time, _max_drink_time))
			current_state = States.DRINKING
		
		# Estado de bebendo
		States.DRINKING:
			# Beber
			_drink()

# FunÃ§Ã£o para a fisica
func _physics_process(delta: float) -> void:
	# Se estiver se movendo atualizar as configuraÃ§Ãµes de movimento
	if current_state == States.MOVING:
		_update_movement()
	move_and_slide()


############ Movimento ###########
# Atualiza as configuraÃ§Ãµes de movimento
func _update_movement() -> void:
	# Proximo destino
	var next_point = path_agent.get_next_path_position()
	var direction = (next_point - global_position).normalized()
	velocity = direction * SPEED

# Define qual o proximo destino
func go_to(target: Vector2) -> void:
	if path_agent.target_position == target:
		return
	path_agent.target_position = target
	current_state = States.MOVING


############ Escolha da proxima aÃ§Ã£o ##############
func _choose_next_action() -> void:
	# Escolhe se quer beber de novo ou ir embora
	var _choose := randi_range(0, 1)
	if _choose <= 0:
		wants_another_drink = true
	else:
		wants_another_drink = false


############ Bar ###########
# Pega o nÃ³ do bar
func _find_the_bar() -> StaticBody2D:
	return get_tree().get_first_node_in_group("Bar")

# Vai atÃ© o bar
func _move_to_bar() -> void:
	var pos = _find_the_bar().global_position + Vector2(0, 50) # Ajuste temporario da posiÃ§Ã£o
	go_to(pos)

# Faz o pedido da bebida
func _request_drink() -> void:
	# Se jÃ¡ tiver pedido, retornar
	if drink_ordered:
		return
	drink_ordered = Drink.new()
	print("%s pediu um drink" % name)
	current_state = States.IDLE

# Recebe a bibida
func take_the_drink(drink: Drink) -> void:
	print("%s foi servido com sucesso" % name)
	drink_ordered = null
	is_served = true
	current_state = States.IDLE


############ Mesas ###########
# Pega as mesas disponiveis
func _get_free_tables(tables: Array) -> Array:
	var _free_tables := []
	for t in tables:
		# Verifica se a mesa tem assentos livres
		if not t.get_node("SeatManager").free_seats.is_empty():
			_free_tables.append(t)
	
	return _free_tables

# Escolhe uma mesa
func _choose_table() -> StaticBody2D:
	# Pega mesas com assentos disponÃ­veis
	var _tables = get_tree().get_nodes_in_group("Tables")
	var _free_tables = _get_free_tables(_tables)

	return _free_tables.pick_random()

func _get_seat(table: StaticBody2D) -> Marker2D:
	var _table_seat_manager: SeatManager = table.get_node("SeatManager")
	return _table_seat_manager.get_free_seat_list().pick_random()

# Vai atÃ© a mesa escolhida
func _move_to_seat() -> void:
	# Se nÃ£o tiver um assento, escolher um
	if not current_seat:
		# se nÃ£o houver mesas livres, ir embora
		var _table = _choose_table()
		if not _table:
			wants_another_drink = false
			is_served = false
			drink_ordered = null
			return
			
		var _seat = _get_seat(_table)
		current_seat = _seat
		
	var _pos = current_seat.global_position
	go_to(_pos)

# Senta-se
func _sit_down() -> void:
	print("%s sentou" % name)
	
	# Define o assento como ocupado
	var _seat_manager: SeatManager = current_seat.get_parent()
	_seat_manager.occupy_seat(current_seat)
	
	current_state = States.SITTING

func _empty_seat() -> void:
	# Define o assento como desocupado
	var _seat_manager: SeatManager = current_seat.get_parent()
	_seat_manager.empty_seat(current_seat)
	current_seat = null

# Toma a bebida
func _drink() -> void:
	# Termina de beber
	if is_drink_finished:
		is_drink_finished = false
		is_served = false
		print("%s terminou a bebida ðŸ¥›" % name)
		
		_choose_next_action()
		current_state = States.IDLE


############ Partida ###########
# Pega o nÃ³ da porta de saÃ­da
func _find_the_door() -> StaticBody2D:
	var _exit_door: StaticBody2D = null
	var _doors := get_tree().get_nodes_in_group("Doors")
	for d in _doors:
		if d.name == "ExitDoor":
			_exit_door = d
	
	if not _exit_door:
		print("%s nÃ£o conseguiu achar a porta de saÃ­da. \nPorta de saÃ­da = %s" % [name, _exit_door])
	return _exit_door

# Vai atÃ© a porta
func _move_to_door() -> void:
	var pos = _find_the_door().global_position
	go_to(pos)

# Sai de cena
func get_out() -> void:
	# Se ainda quiser beber, retornar
	if wants_another_drink:
		print("%s: NÃ£o vou sair. Ainda quero beber! ðŸ¥‚" % [name])
		return
	
	print("%s: Acho que jÃ¡ vou embora..." % [name])
	queue_free()
	# LÃ³gica para sair da taverna


############ Controle de Chegada ###########
# Quando chegar ao destindo
func _on_arrival() -> void:
	if wants_another_drink:
		# Se nÃ£o estiver servido nem tiver pedido uma bebida, estar no estado de solicitaÃ§Ã£o
		if not is_served and not drink_ordered:
			current_state = States.ORDERING
		# Se estiver servido, ir sentar
		elif is_served:
			_sit_down()


########## Timer ###########
func _on_timer_timeout() -> void:
	if not is_drink_finished:
		is_drink_finished = true
